apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {

    lintOptions {
        abortOnError true
        enable 'StopShip'
        fatal 'StopShip'
    }

    dexOptions {
        javaMaxHeapSize "4096M"
        preDexLibraries = false
    }

    compileSdkVersion built.compileSdkVersion
    buildToolsVersion built.buildToolsVersion

    defaultConfig {
        applicationId built.applicationId
        minSdkVersion built.minSdkVersion
        targetSdkVersion built.targetSDKVersion
        versionCode built.versionCode
        versionName built.versionName
        manifestPlaceholders = [
                WEIXIN_APP_ID: keys.WEIXIN_APP_ID_DEBUG,
                WEIBO_APP_KEY: keys.WEIBO_APP_KEY,
                UMENG_APP_KEY: keys.UMENG_APP_KEY,
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources true
            manifestPlaceholders = [
                    WEIXIN_APP_ID: keys.WEIXIN_APP_ID_RELEASE,
                    WEIBO_APP_KEY: keys.WEIBO_APP_KEY,
                    UMENG_APP_KEY: keys.UMENG_APP_KEY,
            ]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = "sourcewall_${variant.buildType.name}_v${defaultConfig.versionName}_${defaultConfig.versionCode}";
                fileName += "_${variant.productFlavors[0].name}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }

            if (output.zipAlign != null) {
                output.zipAlign.doLast {
                    output.zipAlign.inputFile.delete()
                }
            }
        }
    }

    productFlavors {
        qihu360 {
            manifestPlaceholders = [CHANNEL_NAME: "360"]
        }
        baidu {
            manifestPlaceholders = [CHANNEL_NAME: "baidu"]
        }
        xiaomi {
            manifestPlaceholders = [CHANNEL_NAME: "xiaomi"]
        }
        meizu {
            manifestPlaceholders = [CHANNEL_NAME: "meizu"]
        }
        github {
            manifestPlaceholders = [CHANNEL_NAME: "github"]
        }
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }
    buildToolsVersion '23.0.3'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile deps.SupportV4
    compile deps.SupportAppcompat
    compile deps.SupportDesign
    compile deps.CardView
    compile deps.EventBus
    compile deps.GreenDAO
    compile deps.Umeng
    compile deps.OKHttp
    compile deps.OKHttpUrlConnection
    compile deps.ButterKnife
    compile deps.RxAndroid
    compile deps.RxJava
    compile deps.Jsoup
    compile deps.Fab
    compile deps.ScaleImage
    compile deps.GifDrawable
    compile deps.ImageLoader
    compile deps.RxBinding
    compile deps.RxBindingV4
    compile deps.RxBindingV7
    compile deps.Httpmime
    compile deps.Markdown4J
    compile deps.SearchView
    compile deps.BottomBar
    compile deps.Logger
    compile deps.Router

    debugCompile deps.LeakCanaryDebug
    releaseCompile deps.LeakCanaryRelease
    testCompile deps.LeakCanaryTest

    compile project(':mvp')
    apt deps.ButterKnifeCompiler

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:2.0.2-beta'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.robolectric:robolectric:3.1'
    testCompile 'org.robolectric:shadows-multidex:3.1'

    // Android Testing Support Library's runner and rules
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support.test:rules:0.5"

    // Dependencies for Android unit tests
    androidTestCompile "junit:junit:4.12"
    androidTestCompile "org.mockito:mockito-core:2.0.2-beta"
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

    // Espresso UI Testing
    androidTestCompile "com.android.support.test.espresso:espresso-core:2.2.2"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:2.2.2"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:2.2.2"

    // Resolve conflicts between main and test APK:
    androidTestCompile deps.SupportAnnotation
    androidTestCompile deps.SupportV4
    androidTestCompile deps.RecyclerView
    androidTestCompile deps.SupportAppcompat
    androidTestCompile deps.SupportDesign
}